# - Config file for the sas package
# It defines the following variables
#  sas_SCRIPT_DIR   - Python scripts
#  sas_LIBRARIES    - libraries to link against
#  sas_LIBRARY_DIR  - sas library dir
#  sas_BINARY_DIR   - binary directory
# The macro enable_sas is also defined, allowing the user to disable checkers, and to apply clang-modernize or clang-format

# Compute paths
get_filename_component(sas_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

get_filename_component(sas_SCRIPT_DIR "${sas_CMAKE_DIR}/../scripts" ABSOLUTE)
get_filename_component(sas_BINARY_DIR "${sas_CMAKE_DIR}/../bin" ABSOLUTE)
get_filename_component(sas_LIBRARY_DIR "${sas_CMAKE_DIR}/../lib" ABSOLUTE)


FIND_LIBRARY( sas_LIBRARIES NAMES libSas.so PATHS
             ${sas_LIBRARY_DIR}
             NO_DEFAULT_PATH
        )

INCLUDE( FindPackageHandleStandardArgs )
FIND_PACKAGE_HANDLE_STANDARD_ARGS( sas DEFAULT_MSG sas_LIBRARY_DIR sas_SCRIPT_DIR sas_LIBRARIES )

#-----------------------------------------------------------------------------------------------------------------------
# Replaces default cxx compiler with clang and applies coding convention checks.
# Arguments:
# FORMAT: if present, clang-format is called
# MODERNIZE: if present, clang-modernize is called with the options specified
# COMPARISON_REPORT_DIR: the directory in which to place the comparison report files; if none is specified
#   ${CMAKE_BINARY_DIR}/sasreport is used.
# DISABLE_CHECKERs: names of checkers to be disabled.
#-----------------------------------------------------------------------------------------------------------------------
MACRO(enable_sas)
  CMAKE_PARSE_ARGUMENTS (ARG "FORMAT"
                        "MODERNIZE;COMPARISON_REPORT_DIR"
                        "DISABLE_CHECKERS;IGNORE_DIRS" ${ARGN})
  set(CMAKE_CXX_COMPILER ${sas_SCRIPT_DIR}/sas_check)
  message(STATUS "sas enabled.")
  add_definitions(--checkers="sas")
  if(ARG_COMPARISON_REPORT_DIR)
    set(sas_REPORT_DIR ${ARG_COMPARISON_REPORT_DIR})
    add_definitions(--comparison_report_dir="${ARG_COMPARISON_REPORT_DIR}")
  else()
    set(sas_REPORT_DIR ${CMAKE_BINARY_DIR}/sasreport)
    add_definitions(--comparison_report_dir="${CMAKE_BINARY_DIR}/sasreport")
  endif(ARG_COMPARISON_REPORT_DIR)

  if(ARG_MODERNIZE)
    set(MODERNIZE_ON "-modernize_on")
    message(STATUS "Clang modernize ON with arguments: ${ARG_MODERNIZE}")
    add_definitions(--modernize="${ARG_MODERNIZE}")
  endif(ARG_MODERNIZE)
  if(ARG_FORMAT)
    set(FORMAT_ON "-format_on")
    message(STATUS "Check-format ON")
    add_definitions(--check_format)
  endif(ARG_FORMAT)
  if(ARG_DISABLE_CHECKERS)
    message(STATUS "Disabled checkers: ${ARG_DISABLE_CHECKERS}")
    add_definitions(--disabled_checkers="${ARG_DISABLE_CHECKERS}")
  endif(ARG_DISABLE_CHECKERS)
  add_custom_target(report_index ${sas_SCRIPT_DIR}/sas_create_index ${CMAKE_SOURCE_DIR} ${sas_REPORT_DIR} "--ignore_dirs" "${ARG_IGNORE_DIRS}")

  #Generate html diffs for header files:
  file(GLOB_RECURSE HEADER_FILES ${CMAKE_SOURCE_DIR} "*.h" "*.hpp")
  message(STATUS "Running modernize/format on header files")
  foreach(HEADER_FILE ${HEADER_FILES})
    execute_process(COMMAND python ${sas_SCRIPT_DIR}/sas_modernize_format
                    "-filename=${HEADER_FILE}"
                    ${MODERNIZE_ON}
                    ${FORMAT_ON}
                    "-modernize_options=${ARG_MODERNIZE}"
                    "-comparison_report_dir=${sas_REPORT_DIR}")
  endforeach()
ENDMACRO()
