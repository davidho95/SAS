# - Config file for the sas package
# It defines the following variables
#  sas_SCRIPT_DIR   - Python scripts
#  sas_LIBRARIES    - libraries to link against
#  sas_LIBRARY_DIR  - sas library dir
#  sas_BINARY_DIR   - binary directory

# Compute paths
get_filename_component(sas_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

get_filename_component(sas_SCRIPT_DIR "${sas_CMAKE_DIR}/../scripts" ABSOLUTE)
get_filename_component(sas_BINARY_DIR "${sas_CMAKE_DIR}/../bin" ABSOLUTE)
get_filename_component(sas_LIBRARY_DIR "${sas_CMAKE_DIR}/../lib" ABSOLUTE)


FIND_LIBRARY( sas_LIBRARIES NAMES libSas.so PATHS
             ${sas_LIBRARY_DIR}
             NO_DEFAULT_PATH
        )

INCLUDE( FindPackageHandleStandardArgs )
FIND_PACKAGE_HANDLE_STANDARD_ARGS( sas DEFAULT_MSG sas_LIBRARY_DIR sas_SCRIPT_DIR sas_LIBRARIES )

MACRO(enable_sas)
  CMAKE_PARSE_ARGUMENTS (ARG "FORMAT"
                        "MODERNIZE;SUMMARY"
                        "EXCLUDE_DIRS;DISABLE_CHECKERS" ${ARGN})
  message(STATUS "sas enabled. CXX compiler: ${CMAKE_CXX_COMPILER}")
  add_definitions(--checkers="sas")
  if(ARG_MODERNIZE)
    message(STATUS "Clang modernize ON with arguments: ${ARG_MODERNIZE}")
    add_definitions(--modernize="${ARG_MODERNIZE}")
  endif(ARG_MODERNIZE)
  if(ARG_FORMAT)
    message(STATUS "Check-format ON")
    add_definitions(--check_format)
  endif(ARG_FORMAT)
  if(ARG_DISABLE_CHECKERS)
    message(STATUS "Disabled checkers: ${ARG_DISABLE_CHECKERS}")
    add_definitions(--disabled_checkers="${ARG_DISABLE_CHECKERS}")
  endif(ARG_DISABLE_CHECKERS)
  set(CMAKE_CXX_COMPILER ${sas_SCRIPT_DIR}/compile)
ENDMACRO()